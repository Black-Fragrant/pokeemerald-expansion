mapscripts Route1_East_MapScripts
{
        MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE
    [
        VAR_NUVEMA_PICK_STARTER, 8 : Route1_East_EventScript_TurnPlayer
    ]
        MAP_SCRIPT_ON_FRAME_TABLE
    [
        VAR_NUVEMA_PICK_STARTER, 8 : Route1_East_EventScript_CatchingTutorial
    ]
}

script Route1_East_EventScript_TurnPlayer
{
	turnobject (LOCALID_PLAYER, DIR_NORTH)
	end()
}

script Route1_East_EventScript_CatchingTutorial
{
	lockall()
	setvar(VAR_NUVEMA_PICK_STARTER, 9)
	applymovement(LOCALID_ROUTE1_EAST_BIANCA_BOTTOM, moves(walk_in_place_left))
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_BOTTOM, moves(walk_in_place_right))
	applymovement(LOCALID_PLAYER, moves(walk_in_place_right))
	waitmovement(0)
	msgbox("Bianca: Oh! I wonder what will happen!\n"
			"Isn't this so exciting?!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_BIANCA_BOTTOM)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_BOTTOM, moves(walk_in_place_up))
	applymovement(LOCALID_PLAYER, moves(walk_in_place_left))
	waitmovement(0)
	msgbox("Cheren: It sure is!\n"
			"C'mon, the professor's waiting.", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_CHEREN_BOTTOM)
	destroy_speech_bubble()
	closemessage()
	applymovement(LOCALID_ROUTE1_EAST_BIANCA_BOTTOM, moves(walk_up * 6))
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_BOTTOM, moves(walk_up * 6))
	applymovement(LOCALID_PLAYER, moves(walk_up * 6))
	waitmovement(0)
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves( walk_up ))
	waitmovement(0)
	special(RemoveCameraObject)
	delay(10)
	msgbox("Cheren: Professor Juniper, I'm sorry\n"
			"to have kept you waiting.", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_CHEREN_BOTTOM)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	msgbox("Professor Juniper: Now that everyone's\n"
			"here, I'll explain.\p"
			"The Pokédex's pages update automatically\n"
			"whenever you meet a Pokémon!\p"
			"Moreover, it's set up so that\n"
			"you obtain even more information\l"
			"when you catch a Pokémon!\p"
			"To make this clear, I'm going to\n"
			"demonstrate how to catch a Pokémon!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_JUNIPER_CT)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	applymovement(LOCALID_ROUTE1_EAST_BIANCA_BOTTOM, moves(walk_up))
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_BOTTOM, moves(walk_up))
	applymovement(LOCALID_PLAYER, moves(walk_up))
	applymovement(LOCALID_ROUTE1_EAST_JUNIPER_CT, moves(walk_up * 3))
	waitmovement(0)
	delay(10)
	special(SavePlayerParty)
	special(LoadWallyZigzagoon)
	special(StartWallyTutorialBattle)
	waitstate
	special(LoadPlayerParty)
	delay(10)
	applymovement(LOCALID_ROUTE1_EAST_JUNIPER_CT, moves(walk_down * 2))
	waitmovement(0)
	delay(10)
	msgbox("Professor Juniper: Did you see that,\n"
			"just now?\p"
			"Here's how it goes, step by step.\n"
			"First, reduce the Pokémon's HP!\p"
			"Pokémon that still have all of\n"
			"their energy are difficult to catch.\p"
			"If you can, use your Pokémon's moves to\n"
			"make the Pokémon you want to catch\l"
			"fall asleep. Or paralyze it!\p"
			"And to wrap this up in the best way,\n"
			"I have a gift for you--some Poké Balls!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_JUNIPER_CT)
	destroy_speech_bubble()
	closemessage()
    giveitem (ITEM_POKE_BALL, 10)
    call_if_eq (VAR_RESULT, FALSE, Route1_East_EventScript_BagIsFull_Continue)
    closemessage
	msgbox("Professor Juniper: Poké Balls are used\n"
			"for catching Pokémon and for carrying\l"
			"the Pokémon you catch!\p"
			"I'm going on ahead.\n"
			"I'll be waiting for you in Accumula Town!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_JUNIPER_CT)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	applymovement(LOCALID_ROUTE1_EAST_JUNIPER_CT, moves(walk_up * 7))
	waitmovement(0)
	removeobject(LOCALID_ROUTE1_EAST_JUNIPER_CT)
	setflag(FLAG_ROUTE1_EAST_JUNIPER_CT)
	delay(10)
	msgbox("Cheren: She forgot to say that Pokémon\n"
			"jump out at you in the tall grass.", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_CHEREN_BOTTOM)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_BOTTOM, moves(walk_in_place_right))
	waitmovement(0)
	msgbox("I'm heading to Accumula Town, too.", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_CHEREN_BOTTOM)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	applymovement(LOCALID_ROUTE1_EAST_BIANCA_BOTTOM, moves(walk_in_place_left))
	waitmovement(0)
	msgbox("Bianca: I think I'll go, too.\p"
			"I'm looking forward to the next town\n"
			"so I can go shopping for Poké Balls!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_BIANCA_BOTTOM)
	destroy_speech_bubble()
	closemessage()
    delay(10)
	playse(SE_PIN)
	applymovement(LOCALID_ROUTE1_EAST_BIANCA_BOTTOM, moves(emote_exclamation_mark))
	waitmovement(0)
	waitse()
	delay(20)
	msgbox("Bianca: Wait a minute!\p"
			"Hey, listen!\n"
			"{PLAYER}, Cheren,\l"
			"I thought of something fun!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_BIANCA_BOTTOM)
	destroy_speech_bubble()
	closemessage()
    delay(10)
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_BOTTOM, moves(walk_in_place_up))
	waitmovement(0)
	msgbox("Cheren: But we need to get going!\n"
			"I'd imagine the professor's waiting, too.", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_CHEREN_BOTTOM)
	destroy_speech_bubble()
	closemessage()
    delay(10)
	msgbox("Bianca: Would you just listen a sec?\n"
			"Seriously!\p"
			"Why don't we see who can catch\n"
			"the most Pokémon?\p"
			"The person carrying the most Pokémon,\n"
			"including the one received from\l"
			"Professor Juniper, is the winner!",MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_BIANCA_BOTTOM)
	destroy_speech_bubble()
	closemessage()
    delay(10)
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_BOTTOM, moves(walk_in_place_right))
	waitmovement(0)
	msgbox("Cheren: Huh.\n"
			"Actually, that sounds interesting.\p"
			"It will fill up Pokédex pages, so I'm\n"
			"sure it will please the professor, too.\p"
			"OK, then. Until we reach Accumula Town,\n"
			"take care of healing your Pokémon\l"
			"at your own house.", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_CHEREN_BOTTOM)
	destroy_speech_bubble()
	closemessage()
    delay(10)
	call_if_set(FLAG_CHOSE_SNIVY, Route1_East_EventScript_BiancaSaysOshawott)
	call_if_set(FLAG_CHOSE_TEPIG, Route1_East_EventScript_BiancaSaysSnivy)
	call_if_set(FLAG_CHOSE_OSHAWOTT, Route1_East_EventScript_BiancaSaysTepig)
	msgbox("Bianca: Me and {STR_VAR_1}\n"
			"will do the best for sure!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_BIANCA_BOTTOM)
	destroy_speech_bubble()
	closemessage()
    delay(10)
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves( delay_16, walk_slow_down ))
	applymovement(LOCALID_ROUTE1_EAST_BIANCA_BOTTOM, moves( walk_up * 7 ))
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_BOTTOM, moves( walk_up * 7 ))
	waitmovement(0)
	special(RemoveCameraObject)
	removeobject(LOCALID_ROUTE1_EAST_BIANCA_BOTTOM)
	removeobject(LOCALID_ROUTE1_EAST_CHEREN_BOTTOM)
	setflag(FLAG_ROUTE1_EAST_CB_BOTTOM)
	delay(20)
	releaseall()
	end()
}

script Route1_East_EventScript_BagIsFull_Continue
{
    msgbox (gText_TooBadBagIsFull, MSGBOX_DEFAULT)
    return()
}

script Route1_East_EventScript_BiancaSaysOshawott
{
	bufferspeciesname(STR_VAR_1, SPECIES_OSHAWOTT)
    return()
}

script Route1_East_EventScript_BiancaSaysSnivy
{
	bufferspeciesname(STR_VAR_1, SPECIES_SNIVY)
    return()
}

script Route1_East_EventScript_BiancaSaysTepig
{
	bufferspeciesname(STR_VAR_1, SPECIES_TEPIG)
    return()
}

script Route1_East_EventScript_SignPost
{
	msgbox("Route 1", MSGBOX_SIGN)
	end
}

script Route1_East_EventScript_TrainerTips
{
	msgbox("Trainer Tips!\p"
			"Make an effort to talk to all the\n"
			"people you meet during your journey!\p"
			"Chances are they will have something\n"
			"useful to tell you.", MSGBOX_SIGN)
}

script Route1_East_EventScript_GreenShirtGuy
{
	msgbox("Wild Pokémon are hiding in the tall grass.\n"
			"What does this mean for you?\p"
			"It means when you want to battle\n"
			"or capture wild Pokémon,\l"
			"you should go walk in the tall grass.", MSGBOX_NPC, TRUE)
	end
}

script Route1_East_EventScript_Girl
{
	lock
	faceplayer
	goto_if_set(FLAG_RECEIVED_POTION_ROUTE1_EAST, Route1_East_EventScript_Girl_AlreadyGivenPotion)
	msgbox("Is your Pokémon's HP all right?\p"
			"When your Pokémon's HP decreases while\n"
			"you're on the road, it can be\l"
			"a big problem, so I'll give you this.\p"
			"Put it in your Bag's Medicine Case.", MSGBOX_DEFAULT)
	giveitem(ITEM_POTION, 1)
	goto_if_eq (VAR_RESULT, FALSE, Route1_East_EventScript_BagIsFull)
	goto(Route1_East_EventScript_Girl_AlreadyGivenPotion)
	end
}

script Route1_East_EventScript_Girl_AlreadyGivenPotion
{
	msgbox("When your Pokémon's HP decreases,\n"
			"they no longer have the energy to fight.\p"
			"So, don't push your Pokémon too hard,\n"
			"and let them rest often.", MSGBOX_DEFAULT)
	setflag (FLAG_RECEIVED_POTION_ROUTE1_EAST)
	release
	end
}

script Route1_East_EventScript_BagIsFull
{
	msgbox (gText_TooBadBagIsFull, MSGBOX_DEFAULT)
	release
	end
}

script Route1_East_EventScript_Cheren_Top
{
	msgbox("Cheren: {PLAYER}, are you going to\n"
		"compare yourself with Bianca?\p"
		"Both Bianca and I have two Pokémon\n"
		"with us.", MSGBOX_NPC, TRUE)
	end
}

script Route1_East_EventScript_Bianca_Top
{
	lock()
	getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
	call_if_eq (VAR_TEMP_0, 20, NuvemaTown_EventScript_BT_Pos1)
	call_if_eq (VAR_TEMP_0, 21, NuvemaTown_EventScript_BT_Pos2)
	call_if_eq (VAR_TEMP_0, 23, NuvemaTown_EventScript_BT_Pos4)
	call_if_eq (VAR_TEMP_0, 24, NuvemaTown_EventScript_BT_Pos5)
	call_if_eq (VAR_TEMP_0, 25, NuvemaTown_EventScript_BT_Pos6)
	delay(10)
	if (msgbox("Bianca: How about it, {PLAYER}?\n"
	"Do you want to compare which one\l"
	"of us has the most Pokémon with them?", MSGBOX_YESNO, TRUE, LOCALID_ROUTE1_EAST_BIANCA_TOP) == YES)
	{
		destroy_speech_bubble()
		closemessage
		goto(Route1_East_EventScript_Bianca_Top_Compare)
	}
	else
	{
		destroy_speech_bubble()
		closemessage
		goto(Route1_East_EventScript_Bianca_Top_Notyet)
	}
	end()
}

script Route1_East_EventScript_Bianca_Top_Notyet
{
	msgbox("Bianca: Hmm...\n"
			"Guess I'll be waiting here, then.", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_BIANCA_TOP)
	destroy_speech_bubble()
	closemessage()
    delay(10)
	applymovement(LOCALID_PLAYER, moves(walk_down))
	waitmovement(0)
	delay(10)
	release()
	end
}

script Route1_East_EventScript_Bianca_Top_Compare
{
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_TOP, moves(walk_up * 3, walk_in_place_left))
	waitmovement(0)
	delay(10)
	getpartysize
	call_if_eq (VAR_RESULT, 1, Route1_East_EventScript_BT_OnePokemon)
	call_if_eq (VAR_RESULT, 2, Route1_East_EventScript_BT_TwoPokemon)
	call_if_eq (VAR_RESULT, 3, Route1_East_EventScript_BT_ThreePokemon)
	call_if_eq (VAR_RESULT, 4, Route1_East_EventScript_BT_FourPokemon)
	call_if_eq (VAR_RESULT, 5, Route1_East_EventScript_BT_FivePokemon)
	call_if_eq (VAR_RESULT, 6, Route1_East_EventScript_BT_SixPokemon)
	delay(10)
	msgbox("Cheren: Incidentally, if you check your\n"
			"Pokédex, you'll see how many Pokémon\l"
			"you've found and how many\l"
			"you've caught.\p"
			"I'm off to Accumula Town!\n"
			"The professor is waiting.", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_CHEREN_TOP)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	playse(SE_POKENAV_CALL)
	msgbox("The Xtransceiver is ringing.\p"
			"Player picked up the Xtransceiver.", MSGBOX_DEFAULT) //this should be transparent
	waitse()
	closemessage()
	playse(SE_POKENAV_HANG_UP)
	waitse()
	msgbox("Cheren: Well, I'm going on ahead.", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_CHEREN_TOP)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	applymovement(LOCALID_ROUTE1_EAST_CHEREN_TOP, moves(walk_up * 5))
	applymovement(LOCALID_ROUTE1_EAST_BIANCA_TOP, moves(walk_up * 5))
	waitmovement(0)
	delay(20)
	setvar(VAR_NUVEMA_PICK_STARTER, 10)
	removeobject(LOCALID_ROUTE1_EAST_CHEREN_TOP)
	removeobject(LOCALID_ROUTE1_EAST_BIANCA_TOP)
	setflag(FLAG_ROUTE1_EAST_CB_TOP)
	release()
	end()
}

script Route1_East_EventScript_BT_OnePokemon
{
	bufferleadmonspeciesname (STR_VAR_1)
	setvar(VAR_ROUTE1_POKEMON_COUNT, 1)
	msgbox("Bianca: What? What? What?\n"
			"You only have {STR_VAR_1}?!\p"
			"If you don't catch any Pokémon, won't\n"
			"{STR_VAR_1} get lonely?", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_BIANCA_TOP)
	destroy_speech_bubble()
	closemessage()
	return()
}

script Route1_East_EventScript_BT_TwoPokemon
{
	setvar(VAR_ROUTE1_POKEMON_COUNT, 2)
	msgbox("Bianca: Seems the number of Pokémon\n"
			"{PLAYER} has is...\p"
			"Two!\n"
			"We all have the same number, then!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_BIANCA_TOP)
	destroy_speech_bubble()
	closemessage()
	return()
}

script Route1_East_EventScript_BT_ThreePokemon
{
	setvar(VAR_ROUTE1_POKEMON_COUNT, 3)
	goto (Route1_East_EventScript_BT_CompareWin)
}

script Route1_East_EventScript_BT_FourPokemon
{
	setvar(VAR_ROUTE1_POKEMON_COUNT, 4)
	goto (Route1_East_EventScript_BT_CompareWin)
}

script Route1_East_EventScript_BT_FivePokemon
{
	setvar(VAR_ROUTE1_POKEMON_COUNT, 5)
	goto (Route1_East_EventScript_BT_CompareWin)
}

script Route1_East_EventScript_BT_SixPokemon
{
	setvar(VAR_ROUTE1_POKEMON_COUNT, 6)
	goto (Route1_East_EventScript_BT_CompareWin)
}

script Route1_East_EventScript_BT_CompareWin
{
	getpartysize
	buffernumberstring (STR_VAR_2, VAR_RESULT)
	msgbox("Bianca: Wow, {STR_VAR_2}?! That's awesome!\p"
			"Are you excited to have that many\n"
			"Pokémon with you?", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE1_EAST_BIANCA_TOP)
	destroy_speech_bubble()
	closemessage()
	return()
}

script NuvemaTown_EventScript_BT_Pos1
{
	applymovement(LOCALID_PLAYER, moves(walk_right * 2, walk_in_place_up))
	waitmovement(0)
	return()
}

script NuvemaTown_EventScript_BT_Pos2
{
	applymovement(LOCALID_PLAYER, moves(walk_right * 1, walk_in_place_up))
	waitmovement(0)
	return()
}

script NuvemaTown_EventScript_BT_Pos4
{
	applymovement(LOCALID_PLAYER, moves(walk_left * 1, walk_in_place_up))
	waitmovement(0)
	return()
}

script NuvemaTown_EventScript_BT_Pos5
{
	applymovement(LOCALID_PLAYER, moves(walk_left * 2, walk_in_place_up))
	waitmovement(0)
	return()
}

script NuvemaTown_EventScript_BT_Pos6
{
	applymovement(LOCALID_PLAYER, moves(walk_left * 3, walk_in_place_up))
	waitmovement(0)
	return()
}


