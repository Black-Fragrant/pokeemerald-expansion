mapscripts Route2_MapScripts {}


script Route2_EventScript_MotherGivesRunningShoes
{
	lock()
	playse(SE_POKENAV_CALL)
	msgbox("The Xtransceiver is ringing.\p"
			"Player picked up the Xtransceiver.", MSGBOX_DEFAULT) //this should be transparent
	waitse()
	closemessage()
	playse(SE_POKENAV_HANG_UP)
	waitse()
	delay(10)
	playse(SE_EXIT)
	addobject (LOCALID_ROUTE2_MOTHER)
	waitse()
	playbgm(MUS_BW_FOLLOW_ME, TRUE)
	delay(10)
	getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
	call_if_eq (VAR_TEMP_1, 40, Route2_EventScript_MotherGivesRunningShoesPos1)
	call_if_eq (VAR_TEMP_1, 41, Route2_EventScript_MotherGivesRunningShoesPos2)
	call_if_eq (VAR_TEMP_1, 42, Route2_EventScript_MotherGivesRunningShoesPos3)
	msgbox("Mom: {PLAYER}?", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE2_MOTHER)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	applymovement(LOCALID_PLAYER, moves(walk_in_place_right))
	waitmovement(0)
	msgbox("I finally caught up with you...\p"
			"And look how far you've come already!\p"
			"Professor Juniper said that you were\n"
			"probably headed for Striaton City by now.\p"
			"I have another present for you.\n"
			"{PLAYER}! Try these on!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE2_MOTHER)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	applymovement(LOCALID_ROUTE2_MOTHER, moves(walk_left))
	waitmovement(0)
	delay(10)
    playbgm (MUS_BW_OBTAIN_KEY_ITEM, TRUE)
    msgbox("{PLAYER} received a pair of Running Shoes!") //this should be transparent
	setflag(FLAG_SYS_B_DASH)
    delay(120)
	playbgm(MUS_BW_FOLLOW_ME, TRUE)
	msgbox("When you're wearing the Running Shoes,\n"
			"you can go anywhere in a flash!\p"
			"Let me read you the instructions...\p"
			"''Hold the B Button to run faster than\n"
			"normal. Put on the Running Shoes and\l"
			"tear around to your heart's content!''\p"
			"That's what it says.\p"
			"I bought these a long time ago\n"
			"and stashed them away somewhere.\p"
			"I found them when I was cleaning up.\n"
			"Guess it's good to clean occasionally!\p"
			"{PLAYER}, you understand that\n"
			"you're never really alone, right?\p"
			"You're with Pokémon, you have friends,\n"
			"and you're always in my thoughts.\p"
			"That's all right, then. Enjoy your trip!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE2_MOTHER)
	destroy_speech_bubble()
	closemessage()
	delay(10)
	getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
	call_if_eq (VAR_TEMP_1, 40, Route2_EventScript_MotherGivesRunningShoesPos4)
	call_if_eq (VAR_TEMP_1, 41, Route2_EventScript_MotherGivesRunningShoesPos5)
	call_if_eq (VAR_TEMP_1, 42, Route2_EventScript_MotherGivesRunningShoesPos6)
	delay(20)
	playse(SE_EXIT)
	removeobject (LOCALID_ROUTE2_MOTHER)
	waitse()
	playbgm(MUS_BW_ROUTE_1, TRUE)
	clearflag(FLAG_NUVEMA_PH2F_IS_MESSY)
	setvar(VAR_ROUTE2_MOTHER_GIVES_SHOES, 1)
	delay(20)
	release()
	end()
}

script Route2_EventScript_MotherGivesRunningShoesPos1
{
	applymovement(LOCALID_ROUTE2_MOTHER, moves(walk_left, walk_up, walk_left * 3))
	waitmovement(0)
	return()
}

script Route2_EventScript_MotherGivesRunningShoesPos2
{
	applymovement(LOCALID_ROUTE2_MOTHER, moves(walk_left * 4))
	waitmovement(0)
	return()
}

script Route2_EventScript_MotherGivesRunningShoesPos3
{
	applymovement(LOCALID_ROUTE2_MOTHER, moves(walk_left, walk_down, walk_left * 3))
	waitmovement(0)
	return()
}

script Route2_EventScript_MotherGivesRunningShoesPos4
{
	applymovement(LOCALID_ROUTE2_MOTHER, moves(walk_down, walk_right * 5))
	waitmovement(0)
	return()
}

script Route2_EventScript_MotherGivesRunningShoesPos5
{
	applymovement(LOCALID_ROUTE2_MOTHER, moves(walk_right * 5))
	waitmovement(0)
	return()
}

script Route2_EventScript_MotherGivesRunningShoesPos6
{
	applymovement(LOCALID_ROUTE2_MOTHER, moves(walk_up, walk_right * 5))
	waitmovement(0)
	return()
}

script Route2_EventScript_SignPost
{
	msgbox("Route 2", MSGBOX_SIGN)
	end
}

script Route2_EventScript_TrainerTips
{
	msgbox("Trainer Tips!\p"
			"Pokémon that participate in battle\n"
			"receive Exp. Points.\p"
			"Have your Pokémon battle often,\n"
			"and make them stronger and stronger!", MSGBOX_SIGN)
}

script Route2_EventScript_LassBottom
{
	msgbox("Battles between Pokémon Trainers\n"
     		"are serious affairs you can't run from!", MSGBOX_NPC, TRUE)
	end
}

script Route2_EventScript_YoungsterMiddle
{
	msgbox("When your Pokémon don't have\n"
			"enough energy to fight...\l"
			"Just walk on the road!\p"
			"Pokémon only appear in the tall grass!", MSGBOX_NPC, TRUE)
	end
}

script Route2_EventScript_Battle_Youngster1
{
	trainerbattle_single(TRAINER_YOUNGSTER_ROUTE_2_1,
	text("A Trainer catches another Trainer's eye.\n"
    	    "That is the start of a Pokémon battle!"),
	text("What?!\n"
       	    "You are too strong!"))
	msgbox("I'll go into the tall grass and\n"
			"have battles with wild Pokémon!\p"
			"But, before that, I have to visit\n"
			"the Pokémon Center to heal up my team.", MSGBOX_NPC, TRUE)
	end()
}

script Route2_EventScript_Battle_Lass
{
	trainerbattle_single(TRAINER_LASS_ROUTE_2,
	text("I really love Pokémon battles!\p"
    	    "'Cause my cute Pokémon will do its best\n"
			"in a really cute way!"),
	text("Even though it lost,\n"
       		"my Pokémon is still cute!"))
	msgbox("I'll make my cute Pokémon even stronger\n"
       		"by having battles with lots of Trainers!", MSGBOX_NPC, TRUE)
	end()
}

script Route2_EventScript_Battle_Youngster2
{
	trainerbattle_single(TRAINER_YOUNGSTER_ROUTE_2_2,
	text("Lillipup, Lillipup!\n"
       		"My Lillipup! Go!"),
	text("Your Pokémon uses those\n"
       		"kinds of moves?!"))
	msgbox("Different Trainers use\n"
			"different kinds of Pokémon!\l"
			"That's fun to see, isn't it?", MSGBOX_NPC, TRUE)
	end()
}

script Route2_EventScript_BoyOverLedge
{
	lock()
	faceplayer()
	goto_if_set(FLAG_TEMP_13, Route2_EventScript_BoyOverLedge_PostJump)
	msgbox("Hey, you!\p"
			"I'm gonna do something amazing!\n"
			"Wanna watch?", MSGBOX_YESNO, TRUE, LOCALID_ROUTE2_BOY_OVER_LEDGE)
	goto_if_eq(VAR_RESULT, TRUE, Route2_EventScript_BoyOverLedge_Jump)
	destroy_speech_bubble()
	closemessage
	msgbox("No need to be shy!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE2_BOY_OVER_LEDGE)
	destroy_speech_bubble()
	closemessage
	release()
	end()
}

script Route2_EventScript_BoyOverLedge_Jump
{
	destroy_speech_bubble()
	closemessage
	msgbox("OK! Wait one second!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE2_BOY_OVER_LEDGE)
	destroy_speech_bubble()
	closemessage
	applymovement(LOCALID_ROUTE2_BOY_OVER_LEDGE, moves(face_down))
	waitmovement(0)
	msgbox("Wooo!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE2_BOY_OVER_LEDGE)
	destroy_speech_bubble()
	closemessage
	playse(SE_LEDGE)
	applymovement(LOCALID_ROUTE2_BOY_OVER_LEDGE, moves(jump_2_down))
	applymovement(LOCALID_PLAYER, moves(delay_8, face_down))
	waitmovement(0)
	waitse()
	delay(30)
	applymovement(LOCALID_ROUTE2_BOY_OVER_LEDGE, moves(face_up))
	waitmovement(0)
	goto(Route2_EventScript_BoyOverLedge_PostJump)
	end()
}

script Route2_EventScript_BoyOverLedge_PostJump
{
	msgbox("Hey! Were you watching?\p"
			"You can take a shortcut by\n"
			"jumping off ledges! Amazing, eh?", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE2_BOY_OVER_LEDGE)
	destroy_speech_bubble()
	closemessage
	setflag(FLAG_TEMP_13)
	setobjectxyperm(LOCALID_ROUTE2_BOY_OVER_LEDGE, 19, 37)
	release()
	end()
}

script Route2_EventScript_BiancaBattle
{
	lock()
	msgbox("Bianca: Ohhhh! Hi, {PLAYER}!", MSGBOX_DEFAULT)
	closemessage
	delay(10)
	playse(SE_PIN)
	applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark))
	waitmovement(0)
	waitse()
	delay(20)
	playbgm(MUS_BW_BIANCA, TRUE)
	addobject(LOCALID_ROUTE2_BIANCA)
	getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
	call_if_eq (VAR_TEMP_0, 48, Route2_EventScript_BB_Pos1)
	call_if_eq (VAR_TEMP_0, 49, Route2_EventScript_BB_Pos2)
	call_if_eq (VAR_TEMP_0, 50, Route2_EventScript_BB_Pos3)
	call_if_eq (VAR_TEMP_0, 51, Route2_EventScript_BB_Pos4)
	call_if_eq (VAR_TEMP_0, 52, Route2_EventScript_BB_Pos5)
	call_if_eq (VAR_TEMP_0, 53, Route2_EventScript_BB_Pos6)
	applymovement(LOCALID_PLAYER, moves(walk_in_place_left))
	applymovement(LOCALID_ROUTE2_BIANCA, moves(walk_right * 7))
	waitmovement(0)
	applymovement(LOCALID_PLAYER, moves(walk_in_place_down))
	applymovement(LOCALID_ROUTE2_BIANCA, moves(walk_right, walk_up * 3))
	waitmovement(0)
	delay(10)
	msgbox("Bianca: Hey, listen!\n"
			"Let's have a Pokémon battle!\p"
			"Pleeeease? The Pokémon I caught\n"
			"have become a little stronger!\p"
			"Ready or not, here I come!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE2_BIANCA)
	destroy_speech_bubble()
	closemessage
	delay(10)
	call_if_set(FLAG_CHOSE_SNIVY, Route2_EventScript_BiancaBattle_Oshawott)
	call_if_set(FLAG_CHOSE_TEPIG, Route2_EventScript_BiancaBattle_Snivy)
	call_if_set(FLAG_CHOSE_OSHAWOTT, Route2_EventScript_BiancaBattle_Tepig)
	delay(10)
	setvar(VAR_ROUTE2_MOTHER_GIVES_SHOES, 2)
	msgbox("Bianca: Woo-hoo!\p"
			"You are a tough cookie, {PLAYER}.\p"
			"I'm gonna work hard on training my\n"
			"Pokémon so we won't lose anymore!\l"
			"OK, bye-bye!", MSGBOX_DEFAULT, TRUE, LOCALID_ROUTE2_BIANCA)
	destroy_speech_bubble()
	closemessage
	delay(10)
	applymovement(LOCALID_ROUTE2_BIANCA, moves(walk_down * 3))
	waitmovement(0)
	applymovement(LOCALID_PLAYER, moves(walk_in_place_left))
	applymovement(LOCALID_ROUTE2_BIANCA, moves(walk_left * 8))
	waitmovement(0)
	removeobject(LOCALID_ROUTE2_BIANCA)
	delay(20)
	playbgm(MUS_BW_ROUTE_1, TRUE)
	release()
	end()
}

script Route2_EventScript_BB_Pos1
{
	setobjectxy(LOCALID_ROUTE2_BIANCA, 40, 10)
	return()
}

script Route2_EventScript_BB_Pos2
{
	setobjectxy(LOCALID_ROUTE2_BIANCA, 41, 10)
	return()
}

script Route2_EventScript_BB_Pos3
{
	setobjectxy(LOCALID_ROUTE2_BIANCA, 42, 10)
	return()
}

script Route2_EventScript_BB_Pos4
{
	setobjectxy(LOCALID_ROUTE2_BIANCA, 43, 10)
	return()
}

script Route2_EventScript_BB_Pos5
{
	setobjectxy(LOCALID_ROUTE2_BIANCA, 44, 10)
	return()
}

script Route2_EventScript_BB_Pos6
{
	setobjectxy(LOCALID_ROUTE2_BIANCA, 45, 10)
	return()
}

script Route2_EventScript_BiancaBattle_Oshawott
{
	trainerbattle_no_intro (TRAINER_BIANCA_ROUTE_2_1, text("Aww...\nI couldn't win..."))
	return()
}

script Route2_EventScript_BiancaBattle_Snivy
{
	trainerbattle_no_intro (TRAINER_BIANCA_ROUTE_2_2, text("Aww...\nI couldn't win..."))
	return()
}

script Route2_EventScript_BiancaBattle_Tepig
{
	trainerbattle_no_intro (TRAINER_BIANCA_ROUTE_2_3, text("Aww...\nI couldn't win..."))
	return()
}
